<!DOCTYPE html>
<html>

<head>
	<meta charset="utf-8">
	<title>Design-FPS</title>
	<!--顶点Shader-->
	<script id="vertex-shader" type="x-shader/x-vertex">
		const int LightNum = 3;	// 光照数量
		attribute vec3 a_Position;	// 输入三维顶点坐标
		attribute vec3 a_Normal;	// 顶点法向（建模坐标系）
		attribute vec2 a_Texcoord;	// 顶点纹理位置坐标
		varying vec3 v_fN;	// 法向（观察坐标系）
		varying vec3 v_fE;	// 观察向量（观察坐标系）
		varying vec3 v_fL[LightNum];	// 光照向量（观察坐标系）
		varying float v_Dist;	// 顶点到手电筒光源距离		
		varying vec2 v_Texcoord;	// 输出纹理位置坐标到片元shader
		uniform mat4 u_ModelView;	// 模视矩阵
		uniform mat4 u_Projection;	// 投影矩阵
		uniform mat3 u_NormalMat;	// 法向变换矩阵
		uniform vec4 u_LightPosition[LightNum];	// 光源位置(观察坐标系)
		uniform bool u_bOnlyTexture;	// 是否只用纹理
		void main(){
			v_Texcoord = a_Texcoord;
			// 将顶点坐标转到观察坐标系下（在观察坐标系计算光照）
			vec3 pos = (u_ModelView * vec4(a_Position, 1.0)).xyz;

			if(!u_bOnlyTexture){
				v_fE = normalize(-pos);	// 观察者方向向量
				v_fN = normalize(u_NormalMat * a_Normal);// 计算观察坐标系下的法向
				for(int i = 0; i < LightNum; i++){
					if(u_LightPosition[i].w != 0.0){	
						v_fL[i] = normalize(u_LightPosition[i].xyz - pos);//近距离光源
					}
					else{	
						v_fL[i] = normalize(u_LightPosition[i].xyz);//远距离光源
					}
				}
				v_Dist = distance(pos, u_LightPosition[2].xyz);
			}
			// 计算裁剪坐标系下坐标
			gl_Position = u_Projection * vec4(pos, 1.0);	
		}
	</script>

	<!--片元Shader-->
	<script id="fragment-shader" type="x-shader/x-fragment">
		const int LightNum = 3;	// 光照数量
		precision mediump float;	// 浮点数精度为中等
		varying vec3 v_fN;	// 法向（观察坐标系）
		varying vec3 v_fE;	// 观察向量
		varying vec3 v_fL[LightNum];	// 光照向量
		varying float v_Dist;	// 片元到手电筒光源的距离
		varying vec2 v_Texcoord;	// 从顶点shader输入纹理位置坐标
		uniform sampler2D u_Sampler;	// 2D纹理采样器
		uniform float u_Shininess; //高光系数
		uniform bool u_DrawSelectedIF;
		uniform vec4 u_Color;	
		// 光源亮度与材质反射系数的乘积
		uniform vec3 u_AmbientProduct[LightNum];
		uniform vec3 u_DiffuseProduct[LightNum];
		uniform vec3 u_SpecularProduct[LightNum];
		uniform vec3 u_Emission;	// 发射光属性
		uniform float u_Alpha;	// 透明度
		// 聚光灯参数(附属在照相机的电筒光源)
		uniform vec3 u_SpotDirection;	// 聚光灯照射方向(观察坐标系)
		uniform float u_SpotCutOff;		// 聚光灯截止角（角度）
		uniform float u_SpotExponent;	// 聚光灯衰减指数
		uniform bool u_LightSwitch[LightNum];	// 光源开光（数组）
		uniform bool u_bOnlyTexture;	// 是否只用纹理

		void main(){
			if(u_bOnlyTexture){
				gl_FragColor = texture2D(u_Sampler, v_Texcoord);
				return;
			}
			// 归一化输入的向量
			vec3 N = normalize(v_fN);
			vec3 E = normalize(v_fE);
			vec3 fragColor = vec3(0.0, 0.0, 0.0);// 用于累加的光照颜色，初始值为0
			vec3 specular = vec3(0.0, 0.0, 0.0);	// 对镜面光分量单独累加
			// 针对每一个光源进行光照计算,并将计算得到的颜色累加到fragColor上
			for(int i = 0; i < LightNum; i++){
				if(!u_LightSwitch[i]){continue;}	// 光源关闭，不计算该光源的贡献，跳转下一次循环
				vec3 L = normalize(v_fL[i]);
				vec3 H = normalize(L + E);	// 半角向量
				float KSpot = 1.0;	// 受聚光灯影响的衰减系数(1.0为不衰减)
				if(i == 2){
					// 对照射方向归一化并反向（因为L也是从顶点指向光源）
					vec3 spotDir = -normalize(u_SpotDirection);
					float cutoff = radians(u_SpotCutOff);	// 角度转弧度
					float c = dot(L, spotDir);	// 偏离角的cos值
					if(c < cos(cutoff)){	// 偏离角度超过截止角
						KSpot = 0.0;		// 完全衰减
					}
					else{	// 强度衰减正比于c^f
						float d = 1.0 + 0.5 * v_Dist;// d为随距离衰减公式
						KSpot = max(pow(c, u_SpotExponent), 0.0) / d;
					}
				}
				vec3 ambient = u_AmbientProduct[i];	// 环境反射分量

				// 漫反射分量
				float Kd = max(dot(L, N), 0.0);
				vec3 diffuse = KSpot * Kd * u_DiffuseProduct[i];

				// 镜面反射分量(单独计算)
				if(Kd != 0.0){	// dot(L, N) != 0;
					float Ks = pow(max( dot(N, H), 0.0), u_Shininess);
					specular += KSpot * Ks * u_SpecularProduct[i];
				}
				fragColor += ambient + diffuse;	// 累加光照计算颜色
			}
			fragColor += u_Emission;	// 物体自发光
			if(u_DrawSelectedIF){
				gl_FragColor = u_Color;
			}
			else{
				gl_FragColor = vec4(fragColor, 1.0) * texture2D(u_Sampler, v_Texcoord) + vec4(specular, 1.0);// 最终片元颜色
				// 无论纹理图片有无透明度分量，texture2D返回的颜色值始终是vec4类型
				gl_FragColor.a = u_Alpha;	// 透明度传值
			}
		}
	</script>

	<!--新的片元shader程序（obj模型绘制使用）-->
	<script id="fragment-shaderNew" type="x-shader/x-fragment">
		const int LightNum = 3;	// 光照数量
		precision mediump float;	// 浮点数精度为中等
		varying vec3 v_fN;	// 法向（观察坐标系）
		varying vec3 v_fE;	// 观察向量
		varying vec3 v_fL[LightNum];	// 光照向量
		varying float v_Dist;	// 片元到手电筒光源的距离
		varying vec2 v_Texcoord;	// 从顶点shader输入纹理位置坐标
		uniform sampler2D u_Sampler;	// 2D纹理采样器
		// 聚光灯参数(附属在照相机的电筒光源)
		uniform vec3 u_SpotDirection;	// 聚光灯照射方向(观察坐标系)
		uniform float u_SpotCutOff;		// 聚光灯截止角（角度）
		uniform float u_SpotExponent;	// 聚光灯衰减指数
		uniform bool u_LightSwitch[LightNum];	// 光源开光（数组）
		// 3个光源的环境光、漫反射、镜面反射
		uniform vec3 u_AmbientLight[LightNum];
		uniform vec3 u_DiffuseLight[LightNum];
		uniform vec3 u_SpecularLight[LightNum];
		// 物体材质
		uniform vec3 u_Ka;	// 环境光反射系数
		uniform vec3 u_Kd;	// 漫反射系数
		uniform vec3 u_Ks;	// 镜面反射系数
		uniform vec3 u_Ke;	// 发射系数
		uniform float u_Ns;	// 高光系数
		uniform float u_d;	// 透明度

		void main(){
			// 归一化输入的向量
			vec3 N = normalize(v_fN);
			vec3 E = normalize(v_fE);

			// 用于累加的光照颜色，初始值为0
			vec3 fragColor = vec3(0.0, 0.0, 0.0);
			vec3 specular = vec3(0.0, 0.0, 0.0);	// 对镜面光分量单独累加
			// 针对每一个光源进行光照计算
			// 并将计算得到的颜色累加到fragColor上
			for(int i = 0; i < LightNum; i++){
				if(!u_LightSwitch[i]){continue;}	// 光源关闭，不计算该光源的贡献，跳转下一次循环

				vec3 L = normalize(v_fL[i]);
				vec3 H = normalize(L + E);	// 半角向量

				float KSpot = 1.0;	// 受聚光灯影响的衰减系数(1.0为不衰减)
				if(i == 2){
					// 对照射方向归一化并反向（因为L也是从顶点指向光源）
					vec3 spotDir = -normalize(u_SpotDirection);
					float cutoff = radians(u_SpotCutOff);	// 角度转弧度
					float c = dot(L, spotDir);	// 偏离角的cos值
					if(c < cos(cutoff)){	// 偏离角度超过截止角
						KSpot = 0.0;		// 完全衰减
					}
					else{	// 强度衰减正比于c^f
						// d为随距离衰减公式
						float d = 1.0 + 0.5 * v_Dist;
						KSpot = max(pow(c, u_SpotExponent), 0.0) / d;
					}
				}
				vec3 ambient = u_AmbientLight[i] * u_Ka;	// 环境反射分量
				// 漫反射分量
				float Fd = max(dot(L, N), 0.0);
				vec3 diffuse = KSpot * Fd * u_DiffuseLight[i] * u_Kd;
				// 镜面反射分量(单独计算)
				if(Fd != 0.0){	// dot(L, N) != 0;
					float Fs = pow( max( dot(N, H), 0.0), u_Ns);
					specular += KSpot * Fs * u_SpecularLight[i] * u_Ks;
				}
				fragColor += ambient + diffuse;	// 累加光照计算颜色
			}
			fragColor += u_Ke;	// 物体自发光
			gl_FragColor = vec4(fragColor, 1.0) * texture2D(u_Sampler, v_Texcoord) + vec4(specular, 1.0);// 最终片元颜色
			// 无论纹理图片有无透明度分量，texture2D返回的颜色值始终是vec4类型
			gl_FragColor.a = u_d;	// 透明度传值
		}
	</script>
	<script type="text/javascript" src="./Common/webgl-utils.js"></script>
	<script type="text/javascript" src="./Common/initShaders.js"></script>
	<script type="text/javascript" src="./Common/MV.js"></script>
	<script type="text/javascript" src="./Common/ObjModel.js"></script>

	<script type="text/javascript" src="1217.js"></script>
</head>

<body>
	<div id="lock" style="width: 100%; height: 100%; border: 1px solid black;">
		<canvas id="webgl" width="1500px" height="750px" style="position: absolute; z-index: 0;">对不起，你的浏览器不支持HTML5的canvas元素！</canvas>
		<canvas id="hud" width="1500px" height="750px" style="position: absolute; z-index: 1;"></canvas>
	</div>
	<script>
		var canvas = document.getElementById("webgl");
	</script>
</body>

</html>